# Maintainer: Yusuke Sasaki <y_sasaki@nuem.nagoya-u.ac.jp>

_realname=cppcheck
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.72
pkgrel=1
pkgdesc="static analysis of C/C++ code (mingw-w64)"
arch=('any')
url="http://cppcheck.sourceforge.net/"
license=('GPL3')
depends=()
makedepends=("${MINGW_PACKAGE_PREFIX}-docbook-xsl"
             "${MINGW_PACKAGE_PREFIX}-qt5")
optdepends=("${MINGW_PACKAGE_PREFIX}-qt5: cppcheck-gui"
            "${MINGW_PACKAGE_PREFIX}-python2-pygments: cppcheck-htmlreport")
source=("https://github.com/danmar/cppcheck/archive/${pkgver}.tar.gz"
        '0010-change-language-files-path.patch')
sha256sums=('c718949e1ec22a8a0dca7e2953a55c502ef65e53ff9922fb91759388618faa7f'
            'a15a73396a2fa346d29df52c3143b46618454d0bc0f12f0ad891d5a4a856cb56')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 -i "${srcdir}/0010-change-language-files-path.patch"
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  MSYS2_ARG_CONV_EXCL="CFGDIR=" \
  LANG='en_US.UTF-8' \
    make \
      SRCDIR=build \
      CFGDIR=${MINGW_PREFIX}/share/cppcheck/cfg \
      HAVE_RULES=yes \
      LDFLAGS=-lshlwapi RDYNAMIC=

  MSYS2_ARG_CONV_EXCL="CFGDIR=" \
    make man \
      DB2MAN=${MINGW_PREFIX}/share/xml/docbook/xsl-stylesheets-1.78.1/manpages/docbook.xsl \
      CFGDIR=${MINGW_PREFIX}/share/cppcheck/cfg

  cd gui
  lrelease gui.pro
  qmake HAVE_RULES=yes
  MSYS2_ARG_CONV_EXCL="CFGDIR=" \
    make \
      SRCDIR=build \
      CFGDIR=${MINGW_PREFIX}/share/cppcheck/cfg \
      HAVE_RULES=yes
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"

  # LANG='en_US.UTF-8' make test \
  #   SRCDIR=build \
  #   CFGDIR=${MINGW_PREFIX}/share/cppcheck/cfg \
  #   HAVE_RULES=yes \
  #   LDFLAGS=-lshlwapi RDYNAMIC=
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  MSYS2_ARG_CONV_EXCL="CFGDIR=" \
  LANG='en_US.UTF-8' \
    make install \
      DESTDIR="${pkgdir}" \
      PREFIX="${MINGW_PREFIX}" \
      SRCDIR=build \
      CFGDIR=${MINGW_PREFIX}/share/cppcheck/cfg \
      HAVE_RULES=yes

  # man and COPYING
  install -Dpm644 cppcheck.1 "${pkgdir}${MINGW_PREFIX}/share/man/man1/cppcheck.1"
  install -Dpm644 COPYING    "${pkgdir}${MINGW_PREFIX}/share/licenses/cppcheck/COPYING"

  # GUI
  install -m755 build/gui/cppcheck-gui.exe "${pkgdir}${MINGW_PREFIX}/bin"
  install -d "${pkgdir}${MINGW_PREFIX}/share/cppcheck/cfg/lang"
  install -D cfg/* -t "${pkgdir}${MINGW_PREFIX}/share/cppcheck/cfg"
  install -D gui/*.qm -t "${pkgdir}${MINGW_PREFIX}/share/cppcheck/cfg/lang"
}

# vim: set ft=sh ts=2 sw=2 et :
